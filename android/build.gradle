// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")

        // Required to support React Native + Hermes projects
        classpath("de.undercouch:gradle-download-task:4.1.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.10")
    }
}

allprojects {
    configurations.configureEach {
        resolutionStrategy {
            dependencySubstitution {
                // We're building from source, so substitute all known RN/Hermes entities
                // with the source projects.
                // See discord_app/patches/@react-native+gradle-plugin+0.72.11.patch for RN's
                // vanilla dependencySubstitution config that we opt out of.
                def reactNativeProjectName = ":packages:react-native:ReactAndroid"
                def hermesProjectName = ":packages:react-native:ReactAndroid:hermes-engine"

                substitute module("com.facebook.react:react-native") using project(reactNativeProjectName)
                substitute module("com.facebook.react:react-android") using project(reactNativeProjectName)

                substitute module("com.facebook.react:hermes-engine") using project(hermesProjectName)
                substitute module("com.facebook.react:hermes-android") using project(hermesProjectName)
            }
        }
    }
}

